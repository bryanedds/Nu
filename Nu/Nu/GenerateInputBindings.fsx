// Nu Game Engine.
// Copyright (C) Bryan Edds, 2013-2020.

#I __SOURCE_DIRECTORY__
#r "../../packages/Magick.NET-Q8-x64.7.5.0.1/lib/net40/Magick.NET-Q8-x64.dll"
#r "../../packages/Csv.1.0.58/lib/net40/Csv.dll"
#r "../../packages/FParsec.1.0.3/lib/net40-client/FParsecCS.dll" // MUST be referenced BEFORE FParsec.dll!
#r "../../packages/FParsec.1.0.3/lib/net40-client/FParsec.dll"
#r "../../packages/Prime.8.4.0/lib/net472/Prime.dll"
#r "../../packages/Prime.Scripting.8.0.0/lib/net472/Prime.Scripting.exe"
#r "../../packages/Aether.Physics2D.1.5.0/lib/net40/Aether.Physics2D.dll"
#r "../../packages/Nito.Collections.Deque.1.1.0/lib/netstandard2.0/Nito.Collections.Deque.dll"
#r "../../Nu/Nu.Dependencies/SDL2-CS/netstandard2.0/SDL2-CS.dll"
#r "../../Nu/Nu.Dependencies/TiledSharp.1.0.2/lib/netstandard2.0/TiledSharp.dll"
#r "../../Nu/Nu.Math/bin/Debug/netstandard2.0/Nu.Math.dll"
#r "../../Nu/Nu/bin/Debug/net7.0/Nu.exe"

open System
open System.Text.RegularExpressions
open System.Linq
open System.IO
open Prime
open SDL2
open Nu

// this function was stolen and converted from - https://stackoverflow.com/a/46095771
let upperCaseToPascalCase (original : string) =
    let invalidCharsRgx = Regex "[^_a-zA-Z0-9]"
    let whiteSpace = Regex "(?<=\\s)"
    let startsWithLowerCaseChar = Regex "^[a-z]"
    let firstCharFollowedByUpperCasesOnly = Regex "(?<=[A-Z])[A-Z0-9]+$"
    let lowerCaseNextToNumber = Regex "(?<=[0-9])[a-z]"
    let upperCaseInside = Regex "(?<=[A-Z])[A-Z]+?((?=[A-Z][a-z])|(?=[0-9]))"
    let pascalCase =
        // replace white spaces with undescore, then replace all invalid chars with empty string
        invalidCharsRgx.Replace(whiteSpace.Replace(original, "_"), "") |>
        // split by underscores
        (fun (str : string) -> str.Split ([|'_'|], StringSplitOptions.RemoveEmptyEntries)) |>
        // set first letter to uppercase
        (fun (strs : string array) -> strs.Select(fun w -> startsWithLowerCaseChar.Replace (w, fun m -> m.Value.ToUpper ()))) |>
        // replace second and all following upper case letters to lower if there is no next lower (ABC -> Abc)
        (fun (strs : string seq) -> strs.Select (fun w -> firstCharFollowedByUpperCasesOnly.Replace(w, fun m -> m.Value.ToLower ()))) |>
        // set upper case the first lower case following a number (Ab9cd -> Ab9Cd)
        (fun (strs : string seq) -> strs.Select(fun w -> lowerCaseNextToNumber.Replace(w, fun m -> m.Value.ToUpper ()))) |>
        // lower second and next upper case letters except the last if it follows by any lower (ABcDEf -> AbcDef)
        (fun (strs : string seq) -> strs.Select(fun w -> upperCaseInside.Replace(w, fun m -> m.Value.ToLower ())))
    String.Concat pascalCase

let enumEntries (ty : Type) =
    ty.GetEnumNames () |>
    enumerable<string> |>
    Seq.map (fun (name : string) ->
        let name = name.Replace ("SDL_SCANCODE_", "")
        let firstChar = name.[0] // NOTE: elided bounds check because I presume no case where this is possible
        if firstChar >= '0' && firstChar <= '9'
        then "Num" + name
        else name) |>
    flip Seq.zip (ty.GetEnumValues () |> enumerable<int>) |>
    Seq.filter (fun (name, _) -> name <> "SDL_NUM_SCANCODES") |>
    Seq.map (mapFst upperCaseToPascalCase) |>
    List.ofSeq

let enumEntryToCode (entryName : string, entryValue : int) =
    "    | " + entryName + " = " + scstring entryValue

let enumEntriesToCode entries =
    let codes = List.map enumEntryToCode entries
    String.Join ("\n", codes)

let generateBindingsCode codesStr =
    "// Nu Game Engine.\n" +
    "// Copyright (C) Bryan Edds, 2013-2020.\n" +
    "\n" +
    "//*********************************************************************************************//\n" +
    "//                                                                                             //\n" +
    "// NOTE: This code is GENERATED by 'GenerateInputBindings.fsx'! Do NOT edit this code by hand! //\n" +
    "//                                                                                             //\n" +
    "//*********************************************************************************************//\n" +
    "\n" +
    "namespace Nu\n" +
    "open System\n" +
    "open Prime\n" +
    "open Nu\n" +
    "\n" +
    "type KeyboardKey =\n" +
    codesStr +
    "\n"

do
    Directory.SetCurrentDirectory (__SOURCE_DIRECTORY__ + "/bin/Debug")
    let code =
        typeof<SDL.SDL_Scancode> |>
        enumEntries |>
        enumEntriesToCode |>
        generateBindingsCode
    File.WriteAllText ("../../Sdl/SdlInputBindings.fs", code)